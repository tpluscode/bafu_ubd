PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cube: <http://purl.org/linked-data/cube#>
prefix qb: <http://purl.org/linked-data/cube#>
PREFIX bafu: <http://environment.data.admin.ch/ubd/66/qb/>


SELECT ?site ?plot ?municipality ?lv03_x_min ?lv03_y_min ?lv03_x_max ?lv03_y_max ?altitude ?landuse ?parameter ?date ?refPeriod ?survey ?measurement ?unit WHERE {
  GRAPH <http://lindas-data.ch/resource/bafu_ubd> {
    ?measurementUri a qb:Observation ;
      qb:dataSet <http://environment.data.admin.ch/ubd/66/qb/ubd66> ;
      bafu:survey ?surveyUri ;
      bafu:date ?dateUri;
      bafu:refPeriod ?refPeriodUri ;
      bafu:parameter ?parameterUri ;
      bafu:unit ?unitUri ;
      bafu:siteplot ?siteplotUri ;
      bafu:landuse ?landuseUri .

    OPTIONAL {?measurementUri bafu:measurement ?measurement . }

    # resolve labels
    
    ?refPeriodUri rdfs:label ?refPeriod .
    
    ?dateUri rdfs:label ?date .

    ?parameterUri rdfs:label ?parameter .
    FILTER(langMatches(lang(?parameter), "EN"))
    
    ?unitUri rdfs:label ?unit .
    FILTER(langMatches(lang(?unit), "EN"))

    ?landuseUri rdfs:label ?landuse .
    FILTER(langMatches(lang(?landuse), "EN"))
    
    ?surveyUri rdfs:label ?survey .
    
    ?siteplotUri bafu:lv03_y_min ?lv03_y_min ;
      bafu:lv03_y_max ?lv03_y_max ;
      bafu:lv03_x_min ?lv03_x_min ;
      bafu:lv03_x_max ?lv03_x_max ;
      bafu:altitude ?altitude ;
      bafu:plot ?plot ;
      bafu:site ?site ;
      bafu:municipality ?municipality .

  }
}
